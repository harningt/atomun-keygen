import org.ajoberstar.gradle.git.release.opinion.Strategies

buildscript {
    dependencies {
        classpath 'net.sf.proguard:proguard-base:5.2.1'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'org.ajoberstar.release-opinion' version '1.5.1'
    id 'org.ajoberstar.defaults' version '0.6.0'
    id "org.standardout.versioneye" version "1.4.0"
    id 'checkstyle'
    id 'findbugs'
}

group = 'us.eharning.atomun'
description = 'Utility library implementing key generation.'
sourceCompatibility = 1.8

defaults {
    id = 'harningt'

    developers = [
            [id: 'harningt', name: 'Thomas Harning Jr', email: 'harningt@gmail.com']
    ]

    // used by license plugin
    copyrightYears = '2016'
}

release {
    // Adds SNAPSHOT strategy to allowed list to be used by Travis-CI
    versionStrategy Strategies.SNAPSHOT
}

repositories {
    jcenter()
    mavenCentral()
    //maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    findbugs 'com.google.code.findbugs:findbugs:3.0.1'

    compile 'us.eharning.atomun:atomun-core:0.0.2'

    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile 'com.google.code.findbugs:annotations:3.0.1'
    compile 'com.google.code.findbugs:jsr305:3.0.1'

    compile 'org.bouncycastle:bcprov-jdk15on:1.55'

    testCompile 'org.codehaus.groovy:groovy:2.4.7'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'net.trajano.caliper:caliper:1.2.1'
    testCompile 'org.yaml:snakeyaml:1.17'
}

checkstyle {
    toolVersion = '7.1.1'
    configFile = new File(rootDir, 'gradle/checkstyle/checkstyle.xml')
}

jacoco {
    toolVersion = '0.7.7.201606060606'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.destination "${buildDir}/jacocoHtml"
    }
}

sonarRunner {
    toolVersion = '2.4'
}

findbugs {
    sourceSets = [ sourceSets.main ]
}

javadoc {
    options.tags "todo:a:TODO:"
}

apply from: "$rootDir/gradle/maven-publish.gradle"

wrapper {
    gradleVersion = '2.14.1'
}

/* Sanity check that the minimum class-set is from Java 7 */
task proguardCheck(type: proguard.gradle.ProGuardTask, dependsOn: assemble) {
    injars project.jar.archivePath
    libraryjars files(System.getenv("JAVA7_HOME") + "/jre/lib/rt.jar")
    libraryjars files(System.getenv("JAVA7_HOME") + "/jre/lib/jce.jar")
    libraryjars files(configurations.compile)
    // Don't really try to do anything, we just want this tool for it's bytecode lint
    dontshrink
    dontobfuscate
    dontoptimize
    // Don't fail if LambdaMetafactory is missing - it would be handled by Retrolambda
    dontwarn 'java.lang.invoke.LambdaMetafactory'
    // Don't note that these are duplicately imported, different test tools pull in
    dontnote 'javax.annotation.**'
    dontnote 'net.jcip.annotations.**'
}
