import org.ajoberstar.gradle.git.release.opinion.Strategies

buildscript {
    dependencies {
        classpath "net.sf.proguard:proguard-gradle:5.3.3"
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'us.eharning.gradle.defaults' version '0.1.3'
    id "org.jetbrains.kotlin.jvm"  version '1.1.3'
}

group = 'us.eharning.atomun'
description = 'Utility library implementing key generation.'
sourceCompatibility = 1.8

release {
    // Adds SNAPSHOT strategy to allowed list to be used by Travis-CI
    versionStrategy Strategies.SNAPSHOT
}

repositories {
    jcenter()
    //maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    mavenLocal()
}

dependencies {
    findbugs 'com.google.code.findbugs:findbugs:3.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.3"

    compile 'us.eharning.atomun:atomun-core:0.0.3-SNAPSHOT'

    /* NOTE: Using the android version due to intended use, consider replacable w/ 22.0 */
    compile group: 'com.google.guava', name: 'guava', version: '22.0-android'
    compile ('com.google.code.findbugs:annotations:3.0.1u2') {
        force = true
    }
    compile 'com.google.code.findbugs:jsr305:3.0.2'

    compile "com.squareup.okio:okio:1.13.0"

    compile 'org.bouncycastle:bcprov-jdk15on:1.57'

    testCompile 'org.codehaus.groovy:groovy:2.4.12'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'net.trajano.caliper:caliper:1.2.1'
    testCompile 'org.yaml:snakeyaml:1.18'
}

javadoc {
    options.tags "todo:a:TODO:"
}

apply from: "$rootDir/gradle/maven-publish.gradle"

license {
    mapping 'kt', 'SLASHSTAR_STYLE'
}

findbugs {
    excludeFilter file('gradle/findbugs-exclude.xml')
}

wrapper {
    gradleVersion = '4.0'
}

/* Sanity check that the minimum class-set is from Java 7 */
task proguardCheck(type: proguard.gradle.ProGuardTask, dependsOn: assemble) {
    injars project.jar.archivePath
    libraryjars files(System.getenv("JAVA7_HOME") + "/jre/lib/rt.jar")
    libraryjars files(System.getenv("JAVA7_HOME") + "/jre/lib/jce.jar")
    libraryjars files(configurations.compile)
    // Don't really try to do anything, we just want this tool for it's bytecode lint
    dontshrink
    dontobfuscate
    dontoptimize
    // Don't fail if LambdaMetafactory is missing - it would be handled by Retrolambda
    dontwarn 'java.lang.invoke.LambdaMetafactory'
    // Don't note that these are duplicately imported, different test tools pull in
    dontnote 'javax.annotation.**'
    dontnote 'net.jcip.annotations.**'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
